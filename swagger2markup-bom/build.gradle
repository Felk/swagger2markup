description = "swagger2markup bom"

apply plugin: 'maven-publish'

def projectArtifactId = 'swagger2markup'
def url = 'https://github.com/Swagger2Markup/swagger2markup'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                def devs = ['RobWin': 'Robert Winkler',
                            'austek': 'Ali Ustek']

                asNode().appendNode('packaging', 'pom')

                def license = asNode().appendNode('licenses').appendNode('license')
                license.appendNode('name', 'Apache-2.0')
                license.appendNode('url', 'https://github.com/Swagger2Markup/swagger2markup/blob/master/LICENSE.txt')
                license.appendNode('distribution', 'repo')

                def root = asNode()

                root.appendNode('name', "${projectArtifactId}")
                root.appendNode('url', "${url}")
                root.appendNode('description', project.description)

                asNode().appendNode('scm').appendNode('url', 'https://github.com/Swagger2Markup/swagger2markup.git')

                Node deps = asNode().appendNode('dependencyManagement').appendNode('dependencies')
                coreProjects.each {
                    Node dep = deps.appendNode('dependency')
                    dep.appendNode('groupId').value = it.group
                    dep.appendNode('artifactId').value = it.name
                    dep.appendNode('version').value = it.version
                }

                def developers = root.appendNode('developers')
                devs.each {
                    def d = developers.appendNode('developer')
                    d.appendNode('id', it.key)
                    d.appendNode('name', it.value)
                }
            }
        }
    }
}
